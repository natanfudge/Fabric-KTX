plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.serialization"
    id "com.jfrog.bintray"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def total_version = "$mod_version+$minecraft_version"
def githubUrl = "https://github.com/$github_repo"

archivesBaseName = mod_id
version = total_version
group = project.maven_group

minecraft {
}




repositories {
    jcenter()
    maven { url = "http://maven.fabricmc.net/" }
    maven { url = "https://kotlin.bintray.com/kotlinx" }
    maven {url = "https://repo.spongepowered.org/maven"}
    maven { url= 'http://repo1.maven.org/maven2' }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$minecraft_version+$yarn_mappings:v2"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"

     modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "net.fabricmc:fabric-language-kotlin:$fabric_kotlin_version"

    modImplementation("com.lettuce.fudge:fabric-drawer:$fabric_drawer_version")
}

processResources {
    inputs.property "version", total_version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": total_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}


// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

def pomConfig = {
    licenses {
        license {
            name project.license
        }
    }
    developers {
        developer {
            id "fudge"
            name "natan"
            email "natandestroyer100@gmail.com"
        }
    }

    scm {
        url githubUrl
    }
}


// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}


jar {
    from "LICENSE"
}

bintray {
    user = project.hasProperty('bintray_user') ? project.property('bintray_user') : ""
    key = project.hasProperty('bintray_api_key') ? project.property('bintray_api_key') : ""
    publications = ["MavenPublication"]
    publish = true //[Default: false] Whether version should be auto published after an upload
    pkg {
        repo = "libs"
        name = mod_id
        desc = mod_description
        websiteUrl = githubUrl
        issueTrackerUrl = "$githubUrl/issues"
        githubRepo = github_repo //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file
        licenses = [license]
        vcsUrl = githubUrl
        version {
            name = total_version
            released = new Date()
        }
    }

}


publishing {
    publications {
        MavenPublication(MavenPublication) {
            artifact(remapJar)

            artifact(sourcesJar) {
                classifier = "sources"
                builtBy remapSourcesJar
            }
            groupId maven_group
            artifactId mod_id
            version total_version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', mod_description)
                root.appendNode('name', mod_id)
                root.appendNode('url', githubUrl)
                root.children().last() + pomConfig
            }
        }
    }
}


Properties properties = new Properties()
File propertiesFile = new File('gradle.properties')
propertiesFile.withInputStream {
    properties.load(it)
}
properties.put("total_version", total_version)


task updateTemplate(type: Copy) {
    group = "template"
    description = "Switches out variables like \$total_version in .template.md files, and copies the results into .md files."
    from(rootDir)
    include("**/*.template.md")
    filesMatching("**/*.template.md") {
        def extensionLength = ".template.md".length()

        name = sourceName.substring(0, sourceName.length() - extensionLength) + ".md"
        expand(properties)
    }
    destinationDir = rootDir
}

bintrayUpload.dependsOn updateTemplate
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses","-Xuse-experimental=kotlin.time.ExperimentalTime"]
    }
}

